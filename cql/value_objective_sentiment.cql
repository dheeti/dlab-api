MATCH
    (u:User)-[r1:RANKS]-(v:Value),
    (u:User)-[r2:RANKS]-(o:Objective)
WITH
    // bin counts of (Value, Objective) rankings into 4 categories
    // based on whether general sentiment:
    //      (Value,     Objective)
    //      ----------------------
    //      (Disagree,  Disagree)
    //      (Disagree,  Agree)
    //      (Agree,     Disagree)
    //      (Agree,     Agree)
    //
    // Disagree:    rank < 0
    // Agree:       rank >= 0
    
    v.node_id AS v,
    o.node_id AS o,
    r1.rank >= 0 AS r1,
    r2.rank >= 0 as r2,
    count(u.node_id) AS count
ORDER BY
    // order so list of counts generated by COLLECT is ordered
    // [ (D,D), (D,A), (A,D), (A,A) ]
    v, o, r1, r2
WITH
    // mean = number of users who evaluated issue / 4
    v, o, AVG(count) AS mean, COLLECT(count) AS counts
WITH
    // compute mean difference squared for each category
    v, o, mean, counts,
    [ x in counts | (x - mean)^2 ] AS mds
WITH
    // compute standard deviation
    v, o, mean, counts,
    SQRT(REDUCE(total = 0.0, x in mds | total + x) / SIZE(mds)) AS stddev
WITH
    // compute each group counts percent difference from the mean
    v, o, mean, counts, stddev, stddev / mean AS meandev,
    [ x in counts | (x - mean) / TOFLOAT(mean) ] AS pctdev
RETURN
    v AS value,
    o AS objective,
    stddev AS stddev,
    meandev,
    counts,
    pctdev AS pctdev
ORDER BY value, stddev;
